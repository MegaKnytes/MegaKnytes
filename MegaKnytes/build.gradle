plugins {
    id 'maven-publish'
    id 'com.android.library'
    id("com.github.node-gradle.node") version "7.0.2"
}

dependencies {
    api 'org.firstinspires.ftc:RobotServer:10.1.0'
    api 'org.firstinspires.ftc:RobotCore:10.1.0'
    api 'org.firstinspires.ftc:Hardware:10.1.0'
    api 'org.firstinspires.ftc:FtcCommon:10.1.0'

    api 'org.openftc:easyopencv:1.7.3'

    implementation("org.nanohttpd:nanohttpd-websocket:2.3.1") {
        exclude module: "nanohttpd"
    }

    implementation("org.nanohttpd:nanohttpd-apache-fileupload:2.3.1") {
        exclude module: "nanohttpd"
    }

    implementation 'javax.servlet:javax.servlet-api:4.0.1'
}


android {
    compileSdkVersion 34
    defaultConfig {
        minSdkVersion 24
        targetSdkVersion 34
        versionCode 1
        aaptOptions {
            ignoreAssetsPattern '!.svn:!.git:!.ds_store:!*.scc:!CVS:!thumbs.db:!picasa.ini:!_*'
        }
    }

    publishing {
        publications {
            release(MavenPublication) {
                groupId = 'com.MegaKnytes'
                artifactId = 'MegaKnytes'
                version = '1.0.0'
                afterEvaluate {
                    from components.release
                }
            }
        }
    }
}

node {
    nodeProjectDir = file("editor")
    download = true;
}

tasks.register('generateStaticBuild', NpxTask) {
    dependsOn npmInstall
    command = 'react-scripts'
    args = ['build']
    inputs.dir(fileTree("editor/").exclude("node_modules/.cache"))
    outputs.dir('src/main/assets/editor')
}

clean.doFirst {
    file('src/main/assets/editor').deleteDir()
}

//publishToMavenLocal.dependsOn(generateStaticBuild)