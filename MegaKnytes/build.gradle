plugins{
    id 'maven-publish'
    id 'com.android.library'
    id("com.github.node-gradle.node") version "7.0.2"
}

dependencies {
    api 'org.firstinspires.ftc:RobotCore:9.1.0'
    api 'org.firstinspires.ftc:RobotServer:9.1.0'
    api 'org.firstinspires.ftc:Hardware:9.1.0'
    api 'org.firstinspires.ftc:FtcCommon:9.1.0'

    api 'org.openftc:easyopencv:1.7.2'

    implementation("org.nanohttpd:nanohttpd-websocket:2.3.1") {
        exclude module: "nanohttpd"
    }
}


android {
    compileSdkVersion 34
    defaultConfig {
        minSdkVersion 24
        targetSdkVersion 34
        versionCode 1
        aaptOptions {
            ignoreAssetsPattern '!.svn:!.git:!.ds_store:!*.scc:!CVS:!thumbs.db:!picasa.ini:!_*'
        }
    }

    publishing {
        publications {
            release(MavenPublication) {
                groupId = 'com.MegaKnytes'
                artifactId = 'MegaKnytes'
                version = '1.0.0'
                afterEvaluate {
                    from components.release
                }
            }
        }
    }
}

node {
    nodeProjectDir = file("editor")
}

tasks.register('generateStaticBuild', NpxTask) {
    dependsOn npmInstall
    command = 'react-scripts'
    args = ['build']
    inputs.files('package.json', 'package-lock.json')
    inputs.dir("editor/public")
    inputs.dir("editor/src")
    inputs.dir(fileTree("editor/node_modules").exclude(".cache"))
    outputs.dir('src/main/assets/editor')
}

publishToMavenLocal.dependsOn(generateStaticBuild)